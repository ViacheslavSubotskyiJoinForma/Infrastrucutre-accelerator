module "aurora_postgresql" {
  source  = "terraform-aws-modules/rds-aurora/aws"
  version = "9.16.1"

  name              = "${var.project_name}-${var.env}"
  database_name     = var.database_name
  engine            = data.aws_rds_engine_version.postgresql.engine
  engine_mode       = "provisioned"
  engine_version    = data.aws_rds_engine_version.postgresql.version
  storage_encrypted = true
  master_username   = var.master_username

  # Generate random password and store in AWS Secrets Manager
  manage_master_user_password = true

  # Network configuration
  vpc_id                   = data.terraform_remote_state.vpc.outputs.vpc_id
  db_subnet_group_name     = data.terraform_remote_state.vpc.outputs.database_subnet_group_name
  create_db_subnet_group   = false
  create_security_group    = true

  security_group_rules = {
    vpc_ingress = {
      cidr_blocks = data.terraform_remote_state.vpc.outputs.private_subnets_cidr_blocks
      description = "Allow access from private subnets"
    }
  }

  # Monitoring
  monitoring_interval             = var.monitoring_interval
  enabled_cloudwatch_logs_exports = ["postgresql"]

  # Serverless v2 configuration
  serverlessv2_scaling_configuration = {
    min_capacity = var.serverlessv2_min_capacity[var.env]
    max_capacity = var.serverlessv2_max_capacity[var.env]
  }

  instance_class = "db.serverless"
  instances = {
    for i in range(var.instance_count[var.env]) : "instance-${i + 1}" => {}
  }

  # Backup configuration
  backup_retention_period      = var.backup_retention_period[var.env]
  preferred_backup_window      = "03:00-04:00"
  preferred_maintenance_window = "mon:04:00-mon:05:00"

  # Security and availability
  apply_immediately                   = var.apply_immediately
  skip_final_snapshot                 = var.skip_final_snapshot[var.env]
  deletion_protection                 = var.deletion_protection[var.env]
  enable_http_endpoint                = var.enable_http_endpoint
  iam_database_authentication_enabled = var.iam_database_authentication_enabled

  # Tags
  tags = local.tags
}
